
# 1. 结构说明语句

verilog 有 4 种结构结构说明语句：
（l）initial 说明语句；
（2）always 说明语句；
（3）task 说明语句；
（4）function 说明语句。

一个程序模块可以有多个 initial 和 always 过程块。

每个 initial 和 always 说明语句在仿真的一开始同时立即开始执行。initial 语句只执行一次，而 always 语句则是不断地重复活动着，直到仿真过程结束。

但 always 语句后跟着的过程块是否运行，则要看它的触发条件是否满足，如满足则运行过程块一次，再次满足则再运行一次，直至仿真过程结束。


在一个模块中，使用 initial 和 always 语句的次数是不受限制的，它们都是同时开始运行的。

task 和 function 语句可以在程序模块中的一处或多处调用。

## 1.1 initial 语句

已经很熟悉了.

## 1.2 always 语句

格式如下

```v
always 触发条件 语句
```

触发条件有两种形式:

* @(敏感事件列表) 多个事件可用 or 或逗号隔开, * 号则代表所有输入变量
  * 边沿触发 posedge, negedge
  * 电平触发
* wait(表达式) 当表达式为真才执行后面的语句

触发条件可以忽略, 这时 always 将一直处于活动状态。

后面的语句可以是一个块语句, 比如顺序块 begin end, 也可以接独立的单条语句比如赋值语句:

```v
always #10 clk = ~clk;

// 等价于
always begin
    #10;
    clk = ~clk;
end
```

## 1.3 task 和 function 说明语句

task 和 function 说明语句分别用来定义任务和函数，利用任务和函数可以把一个很大的程序模块分解成许多较小的任务和函数便于理解和调试。

输入、输出和总线信号的值可以传入、传出任务和函数。

任务和函数往往还是大的程序模块中在不同地点多次用到的相同的程序段。

学会使用 task 和 function 语句可以简化程序的结构，使程序明白易懂，是编写较大型模块的基本功。

函数和任务的不同:

| 比较点   | 函数                                                 | 任务                                                     |
| -------- | ---------------------------------------------------- | -------------------------------------------------------- |
| 输入     | 函数至少有一个输入，端口声明不能包含 inout 型        | 任务可以没有或者有多个输入，且端口声明可以为 inout 型    |
| 输出     | 函数没有输出                                         | 任务可以没有或者有多个输出                               |
| 返回值   | 函数至少有一个返回值                                 | 任务没有返回值                                           |
| 仿真时刻 | 函数总在零时刻就开始执行                             | 任务可以在非零时刻执行                                   |
| 时序逻辑 | 函数不能包含任何时序控制逻辑                         | 任务不能出现 always 语句，但可以包含其他时序控制，如延时 |语句
| 调用     | 函数只能调用函数，不能调用任务                       | 任务可以调用函数和任务                                   |
| 书写规范 | 函数不能单独作为一条语句出现，只能放在赋值语言的右端 | 任务可以作为一条单独的语句出现语句                       |块中

### 1.3 task

```v
// 任务定义
task 任务名;
    端口及数据类型声明;
    多条语句;
endtask

// 任务调用
任务名 (端口1, 端口2, ..., 端口n);
```

例如下面是一个简单的交通灯时序控制:

```v
module traffic_lights;
    parameter on = 1, off = 0, red_tics = 350, 
        amber_tics = 30, green_tics = 200;
    reg clock, red, amber, green;

    initial begin
        $dumpfile("wave.vcd");
        $dumpvars;
        clock = 0;
        red   = off;
        amber = off;
        green = off;
        #500000 $finish;
    end

    // 控制时序
    always begin
        red = on;
        light(red, red_tics);
        green = on;
        light(green, red_tics);
        amber = on;
        light(amber, amber_tics);
    end

    always #100 clock = ~clock;

    // 定义交通灯开启时间的任务
    task light;
        output color;
        input [31:0] tics;
        begin
            repeat (tics) @(posedge clock);
            color = off;
        end
    endtask
endmodule
```

###### 个人理解

通过 repeat (tics) @(posedge clock); 加深了语句与触发条件的理解;

其实 @(posedge clock); 本身就是一条语句，其作用和 #100 差不多, 在顺序执行时它会卡住直到某个信号的到来才进入下一条语句。

比如触发后再等个一段时间

```v
always begin
    @(posedge clock);
    #100;
    c = ~c;
end

// 可简写成
always @(posedge clock) #100 c = ~c;
```

always 作为一个说明语句，说明了其后的语句总是执行，这时如果第一条语句就是 @, 就会卡住等待某个信号才往下继续执行。

repeat, initial 等也作为一个说明语句，各有其自己的功能，都后接一条语句，要扩展多条语句就得引入块，将多条打包成单条语句的形式。

repeat 说明将重复执行后面的语句多少次，initial 说明初始化时就立刻执行后面的语句，至于执行用多少时间看后面的语句有没有延时之类的。

### 1.4 function 说明语句

函数可返回一个用于表达式的值。

```v
// 函数定义
function 返回值的类型 函数名;
    端口及数据类型声明;
    多条语句;
endfunction

// 函数调用
函数名 (输入1, ..., 输入n);
```
函数的特点:

1）不含有任何延迟、时序或时序控制逻辑
2）至少有一个输入变量
3）只有一个返回值，且没有输出
4）不含有非阻塞赋值语句
5）函数可以调用其他函数，但是不能调用任务

函数在声明时，会隐式的声明一个宽度为 range、 名字为 function_id 的寄存器变量，函数的返回值通过这个变量进行传递。当该寄存器变量没有指定位宽时，默认位宽为 1。

例子: 阶乘函数的定义和调用

```v
module tryfact;
    
    function [31: 0] factorial;
        input [3: 0] operand;
        reg [3: 0] index;

        begin
            factorial = 1;  // 返回值名等于函数本身
            for (index = 1; index <= operand; index = index + 1) begin
                factorial = index * factorial;
            end
        end
    endfunction

    reg [31: 0] result;
    reg [31: 0] n;

    initial begin
        result = 1;
        for (n = 0; n <= 9; n = n + 1) begin
            result = factorial(n);
            $display("n = %d result = %d", n, result);
        end
    end
endmodule
```

#### automatic 修饰的任务或函数

在 Verilog 中，一般函数的局部变量是静态的，即函数的每次调用，函数的局部变量都会使用同一个存储空间。若某个函数在两个不同的地方同时并发的调用，那么两个函数调用行为同时对同一块地址进行操作，会导致不确定的函数结果。

Verilog 用关键字 automatic 来对函数进行说明，此类函数在调用时是可以自动分配新的内存空间的，也可以理解为是可递归的。因此，automatic 函数中声明的局部变量不能通过层次命名进行访问，但是 automatic 函数本身可以通过层次名进行调用。

下面用 automatic 函数，实现阶乘计算的递归版本：

```v
function automatic integer factorial(input integer data);
    factorial = (data >= 2) ? data * factorial(data - 1) : 1;
endfunction
```

当然，用任务也可以实现递归:

```v
task automatic factorial(input reg [31:0] data, output reg [31:0] out);
    if(data >= 2) begin
        factorial(data - 1, out);
        out = data * out;
    end
    else out = 1;
endtask
```

###### 个人理解

值得注意的是, 四个说明语句都是只能跟单个语句，因此如果要写多条语句，则必须将它们包括在块中，至于为什么 task 和 function 说明语句后还有个尾巴 endxx, 估计是因为还有输入输出口。

就像模块模块那样，要有空间声明输入输出。但是又不能像模块那样在任意位置写语句，只能接单条语句， begin end 或 fork join 打包的块语句也被视为单条语句。

# 2. 系统任务

## 2.1 显示任务

\$display 和 $write 任务可用来输出信息。其中 \$display 会自动换行，\$write 则不会自动换行。

格式类似 C 的 printf:

```v
$display(fmt, ...);
$write(fmt, ...);
```

输出格式如下 (支持大写, 如: %h => %H)

| 输出格式 | 说明                                                           |
| -------- | -------------------------------------------------------------- |
| %h       | 以十六进制数的形式输出                                         |
| %d       | 以十进制数的形式输出                                           |
| %o       | 以八进制数的形式输出                                           |
| %b       | 以二进制数的形式输出                                           |
| %c       | 以ASCII码字符的形式输出                                        |
| %v       | 输出网络型数据信号强度                                         |
| %m       | 输出等级层次的名字                                             |
| %s       | 以字符串的形式输出                                             |
| %t       | 以当前的时间格式输出                                           |
| %e       | 以指数的形式输出实型数                                         |
| %f       | 以十进制数的形式输出实型数                                     |
| %g       | 以指数或十进制数的形式输出实型数无论何种格式都以较短的结果输出 |

需注意的是 \$display 和 $write 中输出的宽度默认是自动调整的，要取消这种调整可自定义长度, 例如:

```v
$display("n = %0d result = %0d", n, result);
```

如果输出列表中表达式的值包含有不确定的值或高阻值，其结果输出遵循以下规则：

##### （1）在输出格式为十进制的情况下：
① 如果表达式值的所有位均为不定值，则输出结果为小写的x。
② 如果表达式值的所有位均为高阻值，则输出结果为小写的z。
③ 如果表达式值的部分位为不定值，则输出结果为大写的X。
④ 如果表达式值的部分位为高阻值，则输出结果为大写的Z。

```v
$display("%d", 4'b1000);    // 8
$display("%d", 4'bxxxx);    // x
$display("%d", 4'bzzzz);    // X
$display("%d", 4'b10xx);    // X
$display("%d", 4'b10zz);    // Z
$display("%d", 4'b10xz);    // X
```

##### （2）在输出格式为十六进制和八进制的情况下：
① 每4位二进制数为一组代表一位十六进制数，每3位二进制数为一组代表一位八进制数。
② 如果表达式值相对应的某进制数的所有位均为不定值，则该位进制数的输出的结果为、小写的x。
③ 如果表达式值相对应的某进制数的所有位均为高阻值，则该位进制数的输出结果为小写的z。
④ 如果表达式值相对应的某进制数的部分位为不定值，则该位进制数输出结果为大写的X。
⑤ 如果表达式值相对应的某进制数的部分位为高阻值，则该位进制数输出结果为大写的Z。

```v
$display("%x", 16'b1000);    // 0008
$display("%x", 16'bxxxx);    // xxxx
$display("%x", 16'bzzzz);    // zzzz
$display("%x", 16'b10xx);    // 000X
$display("%x", 16'b10zz);    // 000Z
$display("%x", 16'b10xz);    // 000X
```
##### （3）对于二进制输出格式

直接输出具体的每一位的 0, 1, x, z 值

```v
$display("%b", 8'b1000);    // 00001000
$display("%b", 8'bxxxx);    // xxxxxxxx
$display("%b", 8'bzzzz);    // zzzzzzzz
$display("%b", 8'b10xx);    // 000010xx
$display("%b", 8'b10zz);    // 000010zz
$display("%b", 8'b10xz);    // 000010xz
```

## 2.1 文件操作任务

和 C 基本一致:

```v
module test;

    integer fd, i;

    initial begin
        fd = $fopen("file.txt", "w");
        for (i = 0; i < 100; i = i + 1) begin
            $fwrite(fd, "Hello World %0d\n", i);
        end
        $fclose(fd);
    end
    
    integer n;
    reg [8 * 16 - 1: 0] buffer;

    initial begin
        $dumpfile("wave.vcd");
        $dumpvars;

        buffer = 0;
        n = 1;
        
        #50;
        fd = $fopen("file.txt", "r");
        while (n > 0) begin
            n = $fgets(buffer, fd);
            if(n > 0)
                $display("%s", buffer);
            #50;
        end
        $fclose(fd);
    end

endmodule
```

当然读写文件还有很多形式的接口, 但基本都是 f 开头, 比如 fdisplay, fmonitor, fread, fscanf 等.

## 2.2 显示层次

%m 可用于显示当前模块的层次信息， 例如:

```v
$display("now we in %m");
```

这一特征可以显示全层次路径名，包括模块实例、任务、函数和命名块。

## 2.3 选通显示

选通显示 $strobe 可以确保所有在同一时钟沿赋值的语句得到执行后才显示数据:

```v
always @(posedge clock) begin
    a = b;
    c = d;
end

// 显示上述沿时刻的值
always @(posedge clock) $strobe("a = %b, c = %b", a, c);
```

> 时钟上升沿的值在语句 a = b 和 c = d 执行完之后才显示。
> 
> 如果使用 $display 可能在语句 a = b 和 c = d 之前执行，结果显示不同的值。

## 2.4 值变转储文件

值变转储文件（VCD）是一个ASCII文件，它包含仿真时间、范围与信号的定义以及仿真运行过程中信号值的变化等信息。

```v
$dumpfile("wave.vcd");  // 指定文件名, 如不指定，则由仿真器指定默认名
$dumpvars;              // 没有指定变量范围，则把设计中全部信号都转储

// 转储实例 top 中的信号, 1 表示层次等级, 只转储 top 下的第一层信号
// 即只转储 top 模块中的变量，而不转储在 top 中调用的模块的变量
$dumpvars (1, top);

$dumpvars (0, top.m1);  // 0 表示转储 top.m1 模块下各个层的所有信号

$dumpon;                // 启动转储过程
#100000 $dumpoff;       // 过了 100000 个仿真时间单位后，停止转储过程

$dumpall;               // 生成一个检查点，转储所有 VCD 变量的现行值
```

`$dumpfile` 和 `$dumpvars` 任务通常在仿真开始时指定，`$dumpon`、`$dumpoff` 和 `$dum pall` 任务在仿真过程中控制转储过程。


## 2.5 监控函数 $monitor

任务 $monitor 提供了监控和输出参数列表中表达式或变量值的功能。

当启动一个带有一个或多个参数的 Smonitor 任务时，仿真器则建立一个处理机制，使得每当参数列表中变量或表达式的值发生变化时，整个参数列表中变量或表达式的值都将输出显示。

如果同一时刻，两个或多个参数的值发生变化，则在该时刻只输出显示一次。

例如:
```v
$monitor ($time, , "rxd = %b txd = %b", rxd, txd); // 空参数在输出时显示为空格

// 打开和关闭监控任务
$monitoron;
$monitoroff;
```

## 2.6 时间度量系统函数 \$time 和 $ realtime

在 Verilog 中有两种类型的时间系统函数：\$time 和 $realtime。

用这两个时间系统函数可以得到当前的仿真时刻。

例如: 以下程序打算 16ns 和 32ns 后输出 set 的值

```v
`timescale 10ns/1ns

module test;
    reg set;
    parameter p = 1.6;

    initial begin
        $monitor($time, ,"set=", set);
        #p set = 0;
        #p set = 1;
    end
endmodule
```

但 monitor 这将输出:

```
0 set=x
2 set=0
3 set=1
```

因为 \$time 记录的是时间尺度的倍数，这样将 16ns 和 32ns 输出为 1.6 和 3.2，又因为 $time 输出总是取整，因此输出 2 和 3;

\$realtime 和 \$time 一样，只是输出的是实数，这时输出时间应该是 1.6 和 3.2

## 2.7 \$finish 和 $stop

$finish 是退出仿真器, 返回主操作系统，也就是结束仿真过程。

可带参数:

* 0: 不输出任何信息；
* 1： 输出当前仿真时刻和位置 (默认)
* 2: 输出当前仿真时刻、位置和在仿真过程中所用memory及CPU时间的统计

$stop 会把仿真器设置成暂停模式，在仿真环境给出一个交互式命令提示符，将控制全交给任务。

也可带参数值 (0, 1, 2) 用于输出不同信息。

## 2.8 \$readmemb 和 $readmemh

用来从文件中读取数据到存储器中

这两个系统任务可在仿真的任何时刻被执行使用，其格式如下:

```v
$readmemb("文件名", 存储器名, 起始地址, 结束地址);
```

起始地址和结束地址是可选的.

例如:

```v
module test;
    reg [3:0] memory[0:7];
    integer n;

    initial begin
        $readmemb("file.txt", memory);
        for (n = 0; n <= 7; n = n + 1) 
            $display("%b", memory[n]);
    end
endmodule
```

其中文件 file.txt 是有格式要求的, 例如:

```v
1111 1010 0101 1x1z 1_1_ 1_111

// 或者
1111
1010 
0101 
1x1z 
1_1_ 
1_111
```

即: 由一串二进制数 (通过 $readmemb 读取)或十六进制数 (\$readmemh 读取) 组成, 除此之外只能包含空白位置 (空格、换行、制表符)、注释行 (// 或 /**/) 作为分割符, 数字中不能包含位宽和格式说明。


文件中可使用 @xxx 指定存储地址:

```
@002
11111111

@006
1111zzzz 00001111
```

这样将初始化 memory[2] 和 memory[6]、memory[7] 的数据.

## 2.9 $random

产生随机数

## 2.10 条件执行

\$test$plusargs

```v
module test;
    reg [3:0] memory[0:7];
    integer n;

    initial begin
        if ($test$plusargs("DISPLAY_VAR"))
            $display("Display Var");
        else
            $display("No Display");
    end
endmodule
```

仅当在运行时设置了标志 DISPLAYVAR 时才显示变量。可以指定 +DISPLAY_VAR 选项在程序运行时设置标志。

【个人注】(但是在 iverilog 中找不到怎么指定 +DISPLAY_VAR 的方法)

\$value$plusargs 可进一步控制条件执行:

```v
if($value$plusargs("clk_t = %d", clk_period))
```

需要使用带 +clk_t=10 参数，这时 clk_period 将被赋值 10.

# 3. 预处理

## 3.1 宏定义

```v
`define N 32

// 宏展开后将 N 替换为 32
reg [N - 1: 0] a;
```

## 3.2 文件包含 `include 

和 C 一样将一个文件的全部内容复制插到指定位置。

```v
`include "f1.v"
```

## 3.3 时间尺度

```v
`timescale 时间单位/时间精度
```

## 3.4 条件编译

```v
`ifdef 宏名

`else

`endif
```
